# exp2ddl.awk
#
# Convert a file generated by the INDEXFILE option of import to ddl.
#
# Version             Comments (All by kquinn.uk unless noted)
# 1.0.0.b.............Publicised
# 1.0.0.c.............Basic Partition Support 
#		      Note that we now leave the tablespace name IN
# 1.0.0.d.............Support SQL*Plus and correct trailing TABLESPACE issue
#
BEGIN		{ INSTORAGE=1; 
		  printf("rem Exp2ddl.awk: version 1.0.0.d\n\n"); }

/^REM  /	{ sub("^REM  ", ""); }
/^ *$/		{ next }
/\.\.\..*rows/  { printf("REM %s rows\n", $2); next }
/^CONNECT/	{ next }

		### { printf("-->%s\n", $0); }

/STORAGE.*TABLESPACE/	{ sub("STORAGE.*TABLESPACE", " TABLESPACE"); 
                          sub(" *$", "");
                          if ($0 != " TABLESPACE")  # handle trailing tspace
		  	    state=INSTORAGE;        # storage has been dealt
                                                    # with.
                          print; next }

/STORAGE/	{ sub("STORAGE.*$", ""); 
		  state=INSTORAGE; 
		  if ($0 != "") print; 
		  next }

/TABLESPACE/ && state==INSTORAGE 	{ sub(".*TABLESPACE", " TABLESPACE"); 
					  state=0; }

/^CREATE/		{ printf("\nREM ==================>\n"); state=0; }

state==INSTORAGE	{ next }

/^CREATE *TABLE/		{ tabcount++; 
				  sub("TABLE *\".*\"\.\"", "TABLE \"");}
/^CREATE *INDEX/		{ nuindcount++; 
				  sub("INDEX *\".*\"\.\"", "INDEX \"");}
/^CREATE *UNIQUE *INDEX/	{ uindcount++; 
				  sub("INDEX *\".*\"\.\"", "INDEX \"");}

			{ print }

END		{ printf("\n");
		  tot=nuindcount+tabcount+uindcount;
		  printf("rem Objects found      %4d\n", tot);
		  printf("rem Non-Unique Indexes %4d\n", nuindcount);
		  printf("rem Unique     Indexes %4d\n", uindcount);
		  printf("rem Tables             %4d\n", tabcount); }
		
